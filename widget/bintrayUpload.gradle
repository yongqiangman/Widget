// part A:增加gradle插件和版本号
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
version = "0.0.0"  // 项目版本号，每次都要修改

def siteUrl = 'https://github.com/ReactiveX/RxJava'      // 项目的主页
def gitUrl = 'https://github.com/ReactiveX/RxJava.git'   // Git仓库的url
def libName = "Widget" // 对应之前Repository创建的widget package
def libDescription = 'Widget For Android'
def docTitle = "WidgetDoc"
def packageType = 'aar'
group = "com.yqman.widget" // 一般使用包名
// 自己项目的前缀, 对应jcenter地址为：http://jcenter.bintray.com/yqman/maven/widget/X.X.X；
// 对应gradle中使用：dependencies { compile 'com.yqman.widget:Widget:0.0.0'}

// part B:生成POM文件build脚本, This generates POM.xml with proper parameters
install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging packageType
                name libDescription
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'yongqiangman'
                        name 'yongqiangman'
                        email 'yongqiangman@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

// part C:javadoc和sources文件的生成
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
//添加UTF-8编码否则注释可能JAVADOC文档可能生成不了
javadoc {
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
        title docTitle
    }
}

// partD: 自动上传的脚本
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = libName       // project name in jcenter
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}